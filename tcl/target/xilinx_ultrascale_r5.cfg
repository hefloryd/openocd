#
# Target configuration for Xilinx Zynq UltraScale+ R5 cores
# NOTE: The cores must be powered up already, probably by your FSBL
#
if { [info exists CHIPNAME] } {
    set _CHIPNAME $CHIPNAME
} else {
    set _CHIPNAME uscale
}

#
# DAP tap
#
if { [info exists DAP_TAPID] } {
    set _DAP_TAPID $DAP_TAPID
} else {
    set _DAP_TAPID 0x5ba00477
}

jtag newtap $_CHIPNAME tap -irlen 4 -ircapture 0x1 -irmask 0xf -expected-id $_DAP_TAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.tap

#
# PS tap
#
if { [info exists PS_TAPID] } {
    set _PS_TAPID $PS_TAPID
} else {
    set _PS_TAPID 0x14738093
}

set jtag_configured 0

jtag newtap $_CHIPNAME ps -irlen 12 -ircapture 0x1 -irmask 0x03 -expected-id $_PS_TAPID -ignore-version

jtag configure $_CHIPNAME.ps -event setup {
    global _CHIPNAME
    global jtag_configured

    if { $jtag_configured == 0 } {
        # add the DAP tap to the chain
        # See https://forums.xilinx.com/t5/UltraScale-Architecture/JTAG-Chain-Configuration-for-Zynq-UltraScale-MPSoC/td-p/758924
        irscan $_CHIPNAME.ps 0x824
        drscan $_CHIPNAME.ps 32 0x00000003
        runtest 100

        # setup event will be re-entered through jtag arp_init
        # break the recursion
        set jtag_configured 1
        # re-initialized the jtag chain
        jtag arp_init
    }
}

set _TARGETNAME $_CHIPNAME.r5

set _cores 2

for { set _core 0 } { $_core < $_cores } { incr _core } {

    set _command "target create $_TARGETNAME.$_core cortex_r4 -dap $_CHIPNAME.dap"

    if { $_core != 0 } {
        # non-boot core examination may fail
        set _command "$_command -defer-examine"
    }

    eval $_command
    $_TARGETNAME.$_core configure -event reset-assert-post "cortex_r4 dbginit"
    $_TARGETNAME.$_core configure -event gdb-attach "r5_attach"
}

targets $_TARGETNAME.0

proc core_up { args } {
    global _TARGETNAME
    foreach { core } [set args] {
        $_TARGETNAME.$core arp_examine
    }
}

proc r5_attach {} {
   halt
   cortex_r4 maskisr on
}
